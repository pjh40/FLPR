%language=C++
%struct-type
%{
/*
   Copyright (c) 2019, Triad National Security, LLC. All rights reserved.

   This is open source software; you can redistribute it and/or modify it
   under the terms of the BSD-3 License. If software is modified to produce
   derivative works, such modified software should be clearly marked, so as
   not to confuse it with the version available from LANL. Full text of the
   BSD-3 License can be found in the LICENSE file of the repository.
*/

/*!
  \file Smash_Hash.hh
  Perfect hash function developed to split names that are actually
  adjacent keywords without a space, as per section 6.3.2.2.  This file
  was generated by GNU gperf 3.1 using the input found in
  $FLPR/docs/Smash_Hash.gperf.

  NOTE: doubleprecision is NOT included in this! The parsers handle it
        directly by combining DOUBLE PRECISION under a KW_DOUBLEPRECISION
	root.
*/
#ifndef SMASH_HASH_HH
#define SMASH_HASH_HH 1

#include <flpr/Syntax_Tags.hh>
#include <string.h>
namespace FLPR {
namespace details_ {
%}
struct Smashed { char const * name; int tok1; int tok2; int splitpos; };
%%
blockdata, Syntax_Tags::KW_BLOCK, Syntax_Tags::KW_DATA, 5
elseif, Syntax_Tags::KW_ELSE, Syntax_Tags::KW_IF, 4
elsewhere, Syntax_Tags::KW_ELSE, Syntax_Tags::KW_WHERE, 4
endassociate, Syntax_Tags::KW_END, Syntax_Tags::KW_ASSOCIATE, 3
endblock, Syntax_Tags::KW_END, Syntax_Tags::KW_BLOCK, 3
endcritical, Syntax_Tags::KW_END, Syntax_Tags::KW_CRITICAL, 3
enddo, Syntax_Tags::KW_END, Syntax_Tags::KW_DO, 3
endenum, Syntax_Tags::KW_END, Syntax_Tags::KW_ENUM, 3
endfile, Syntax_Tags::KW_END, Syntax_Tags::KW_FILE, 3
endforall, Syntax_Tags::KW_END, Syntax_Tags::KW_FORALL, 3
endfunction, Syntax_Tags::KW_END, Syntax_Tags::KW_FUNCTION, 3
endif, Syntax_Tags::KW_END, Syntax_Tags::KW_IF, 3
endinterface, Syntax_Tags::KW_END, Syntax_Tags::KW_INTERFACE, 3
endmodule, Syntax_Tags::KW_END, Syntax_Tags::KW_MODULE, 3
endprocedure, Syntax_Tags::KW_END, Syntax_Tags::KW_PROCEDURE, 3
endprogram, Syntax_Tags::KW_END, Syntax_Tags::KW_PROGRAM, 3
endselect, Syntax_Tags::KW_END, Syntax_Tags::KW_SELECT, 3
endsubmodule, Syntax_Tags::KW_END, Syntax_Tags::KW_SUBMODULE, 3
endsubroutine, Syntax_Tags::KW_END, Syntax_Tags::KW_SUBROUTINE, 3
endteam, Syntax_Tags::KW_END, Syntax_Tags::KW_TEAM, 3
endtype, Syntax_Tags::KW_END, Syntax_Tags::KW_TYPE, 3
endwhere, Syntax_Tags::KW_END, Syntax_Tags::KW_WHERE, 3
goto, Syntax_Tags::KW_GO, Syntax_Tags::KW_TO, 2
selectcase, Syntax_Tags::KW_SELECT, Syntax_Tags::KW_CASE, 6
selecttype, Syntax_Tags::KW_SELECT, Syntax_Tags::KW_TYPE, 6
%%
} // details_
} // FLPR

#endif
